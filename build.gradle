import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'com.gradleup.shadow' version '9.2.2'
}

group = 'de.skriptlibrary.SkLibrary'
version = '0.0.1'
def targetJavaVersion = 21

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")

    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")
    
    implementation(group: 'org.bstats', name: 'bstats-bukkit', version: '3.1.0')
}

compileJava {
    options.encoding = 'UTF-8'
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.8")
        
        downloadPlugins {
            github("SkriptLang", "Skript", "2.13.1", "Skript-2.13.1.jar")
            modrinth("plugmanx", "3.0.2")
        }
    }
}

build {
    dependsOn(shadowJar)
}

processResources {
    filter ReplaceTokens, tokens: ["version": project.version ]

    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName = project.name + "-" + project.version + ".jar"
    
    relocate("org.bstats", "de.skriptlibrary.SkLibrary.metrics")
    
    exclude('META-INF/**')
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}